con
        r0 = 23
        r1 = 22
        r15 = 8
        sp = 24
        lr = $1f6

dat
        orgh    $400

        ' int setjmp(jmp_buf env)
        ' save registers to env, and return 0

        .global _setjmp
_setjmp
        wrlong  lr, r0
        add     r0, #4
        mov     r15, sp
        setq    #7
        wrlong  r15, r0
        mov     r0, #0
        jmp     lr

        ' int longjmp(jmp_buf env, int val)
        ' restore registers from env, and return val
        ' (which must be non-zero)

        .global _longjmp
_longjmp
        cmp     r1, #0 wz
   if_z mov     r1, #1
        rdlong  lr, r0
        add     r0, #4
        setq    #7
        rdlong  r15, r0
        mov     sp, r15
        mov     r0, r1
        jmp     lr
